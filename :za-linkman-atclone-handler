:za-linkman-atclone-handler() {

emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

if [[ "$1" = plugin ]] {
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
} else {
    local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
}

if (( ${+ICE[lman]} )) {
    [[ -d ${ZPFX}/man/man1 ]] || \
        { print -P -- "%F{38}linkman annex: %F{160}Missing ${ZPFX}/man/man1, not installing man pages%f" && return}
    local manfiles=${dir}/**/*(.1|.1.gz)(N.)
    declare -a manfile installed attempted failed
    for manfile ( $~manfiles ); do
        (( ${attempted[(I)$manfile:t]} )) && continue
        attempted+=( $manfile:t )
        { test -f $manfile } && {
            installed+=( "%F{219}$manfile:t" )
            command ln -f -- $manfile ${ZPFX}/man/man1
        } || {
            failed+=( "%F{219}$manfile:t" )
        }
    done
    (( !OPTS[opt_-q,--quiet] )) && {
        (( ${#installed} )) && \
            print -P -- "%F{38}linkman annex: %F{154}Installed man page${${${#installed}/1/}:+s}: ${(j:%f, :)installed}%f"
        (( ${#failed} )) && \
            print -P -- "%F{38}linkman annex: %F{160}Could not install man page${${${#failed}/1/}:+s}: ${(j:%f, :)failed}%f"
    }
}

}

# vim:ft=zsh:sw=4:sts=4:et
